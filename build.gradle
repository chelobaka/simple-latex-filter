plugins {
    id 'java'
    id 'checkstyle'
    id 'distribution'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.omegat:omegat:6.0.0'
    implementation 'commons-io:commons-io:2.20.0'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'org.netbeans.api:org-openide-awt:RELEASE125'
    implementation "com.fasterxml.jackson.core:jackson-core:2.19.2"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.19.2"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
    testImplementation 'org.xmlunit:xmlunit-assertj3:2.10.3'
    testImplementation 'com.j2html:j2html:1.6.0'
}

test {
    useJUnitPlatform()
}

// Internal tests task
tasks.register('testInternal', Test) {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
    useJUnitPlatform {
        includeTags("internal")
    }
}

tasks.testInternal.setGroup('Verification')

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

jar {
    manifest {
        // plugin's main class name is defined in gradle.properties file.
        attributes(
            "OmegaT-Plugins": pluginMainClass,
            "Plugin-Version": pluginVersion,
            "Plugin-Name": pluginName,
            "Plugin-Author": pluginAuthor,
            "Plugin-Description": pluginDescription
        )
    }

    archiveFileName = "${rootProject.name}-${pluginVersion}.jar"
}

/* Checkstyle */
tasks.checkstyleMain.setGroup('Verification')
tasks.checkstyleTest.setEnabled(false)


artifacts {
    archives jar
}

distZip {
    archiveFileName = "${rootProject.name}-${pluginVersion}.zip"
}

distTar {
    compression = Compression.GZIP
}

distributions {
    main {
        contents {
            from('README.md', 'CONFIGURATION.md', 'COPYING')
            from('util') {
                into 'util'
            }

            from(jar) {
                into 'plugin'
            }
        }
    }
}